#
# $Id: world.dem,v 1.3 1999/06/10 20:05:05 lhecking Exp $
#
#
set title "Gnuplot Correspondences"
unset key
unset border
unset yzeroaxis
unset xtics
unset ytics
#
# plot world map and correspondent locations as a +
plot 'world.dat' with lines 3 4, 'world.cor' with points 1 2
set title ""
set key
set border
set yzeroaxis
set xtics
set ytics
pause -1 "Hit return to continue"
#
# plot a '3D version using spherical coordinate system' of the world.
set angles degrees
set title "3D version using spherical coordinate system"
set ticslevel 0
set view 70,40,0.8,1.2	# HBB: ,,2.0
set mapping spherical
set parametric
set samples 32
set isosamples 9
set urange [-90:90]
set vrange [0:360]
splot cos(u)*cos(v),cos(u)*sin(v),sin(u) with lines 5 6,\
'world.dat' with lines 3 4, 'world.cor' with points 1 2
pause -1 "Hit return to continue"
#
# plot a '3D version using cylindrical coordinate system' of the world.
set title "3D version using cylindrical coordinate system"
set ticslevel 0.0
set view 70,40,0.8,1.2	#HBB: ,,2.0
set mapping cylindrical
set parametric
set samples 32
set isosamples 9
set urange [-180:180]
set vrange [-90:90]
splot cos(u),sin(u),v with lines 5 6,\
'world.dat' with lines 3 4, 'world.cor' with points 1 2
pause -1 "Hit return to continue"


#
# Clean up:
#
reset
