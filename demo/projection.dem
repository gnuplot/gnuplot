unset key
unset colorbox

set walls
set xyplane 0.0
set isosamples 75, 25
set palette cubehelix
set parametric
set urange [ 0.00000 : 31.4159 ]
set vrange [ 0.00000 : 6.28319 ]
set xrange [-5:5]
set yrange [-5:5]

set pm3d implicit at s
set pm3d depthorder
set pm3d border linecolor rgb "black"  linewidth 0.500 dashtype solid

set multiplot layout 2,2 title "2D projections of a 3D surface" font ":Bold"

set title "general view      "
set view 60, 160, 1, 1.2
unset tics

set arrow 1 from graph 0,0,0 to graph 1.20,0,0 filled size graph .05, 15
set arrow 2 from graph 0,0,0 to graph 0,1.25,0 filled size graph .05, 15
set arrow 3 from graph 0,0,0 to graph 0,0,1.25 filled size graph .05, 15
set label 1 at graph 1.30,0,0 "X" center
set label 2 at graph 0,1.35,0 "Y" center
set label 3 at graph 0,0,1.35 "Z" center

splot (1-0.1*u*cos(v))*cos(u),(1-0.1*u*cos(v))*sin(u),0.1*(sin(v)+u/1.7-10) with pm3d

unset for [i=1:3] arrow i
unset for [i=1:3] label i
unset view

set title "set view projection xz"
set xlabel "X-axis" offset 0,0
set ylabel "Y-axis" offset 0,0
set zlabel "Z-axis" offset 0,0
set xlabel rotate by -270
set xtics -4,2,4
set ytics -4,2,4
set ztics 0.5
set tics nomirror scale 0.5, 0.5
set view projection xz
replot

set xlabel norotate

set title "set view projection yz"
set view projection yz
replot

set title "set view map"
set view projection xy
replot

unset multiplot

pause -1 "Hit return to continue"
reset
