#
# Gnuplot version 4.1 demo of multiplot
# auto-layout capability
#
#
set multiplot layout 3, 1 title "Multiplot layout 3, 1"
set title "Plot 1"
plot sin(x)/x
#
set title "Plot 2"
plot 'silver.dat' using 1:2 ti 'silver.dat'
#
set style histogram columns
set style fill solid
set key autotitle column
set boxwidth 0.8
set format y "    "
set tics scale 0
set title "Plot 3"
plot 'immigration.dat' using 2 with histograms, \
     '' using 7  with histograms , \
     '' using 8  with histograms , \
     '' using 11 with histograms 
#
unset multiplot
#
#
#
pause -1 "<cr> to continue"
reset
set multiplot layout 1,3 title "Multiplot layout 1, 3"
set xtics rotate
set bmargin 5
#
set title "Plot 1"
plot sin(x)/x
#
set title "Plot 2"
plot 'silver.dat' using 1:2 ti 'silver.dat'
#
set title "Plot 3"
set style histogram columns
set style fill solid
set key autotitle column
set boxwidth 0.8
set format y "    "
set tics scale 0
plot 'immigration.dat' using 2 with histograms , \
     '' using  7 with histograms , \
     '' using  8 with histograms , \
     '' using 11 with histograms 
#
unset multiplot
#
#
#
#
pause -1 "<cr> to continue"
reset
set multiplot layout 1,3 title "Same plot with a multi-line title\nshowing adjustment of plot area\n to accommodate it"
set xtics rotate
set bmargin 5
#
set title "Plot 1"
plot sin(x)/x
#
set title "Plot 2"
plot 'silver.dat' using 1:2 ti 'silver.dat'
#
set title "Plot 3"
set style histogram columns
set style fill solid
set key autotitle column
set boxwidth 0.8
set format y "    "
set tics scale 0
plot 'immigration.dat' using 2 with histograms , \
     '' using  7 with histograms , \
     '' using  8 with histograms , \
     '' using 11 with histograms 
#
unset multiplot
pause -1 "<cr> to continue"
reset
